<%INIT>
my @results;

my $ACL = RT::ACL->new($session{CurrentUser});

my $has_search = 0;

if ($ARGS{right}) {
    $has_search = 1;
    $ACL->Limit(
        FIELD         => 'RightName',
        OPERATOR      => 'LIKE',
        VALUE         => $ARGS{right},
        CASESENSITIVE => 0,
    );
}

$ACL->UnLimit unless $has_search;

ACE: while (my $ACE = $ACL->Next) {
    my $serialized = RT::Extension::RightsDebugger->SerializeACE($ACE);

    # this is hacky, but doing the searching in SQL is absolutely a nonstarter
    for my $key (qw/principal object/) {
        if (my $search = $ARGS{$key}) {
            my $re = qr/\Q$search\E/i;
            my $record = $serialized->{$key};
            next ACE unless $record->{class}  =~ $re
                         || $record->{id}     =~ $re
                         || $record->{label}  =~ $re
                         || $record->{detail} =~ $re;
        }
    }

    push @results, $serialized;
}

$r->content_type('application/json; charset=utf-8');
$m->out(JSON({ count => $ACL->Count, results => \@results }));
$m->abort;
</%INIT>
